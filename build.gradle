buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath('com.diffplug.gradle.spotless:spotless:2.4.1',
                'com.github.jengelman.gradle.plugins:shadow:1.2.4',
                'com.bmuschko:gradle-docker-plugin:3.0.6')
    }
}

ext.flinkVersion = '1.2.0'

allprojects {
    apply plugin: 'groovy'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'findbugs'

    group 'com.tngtech'
    version '0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        maven {
            url {
                'https://repository.apache.org/content/repositories/snapshots/'
            }
        }
        jcenter()
    }

    dependencies {
        compile 'com.google.guava:guava:20.0'
        compileOnly 'org.projectlombok:lombok:1.16.16'
        testCompile('org.spockframework:spock-core:1.0-groovy-2.4',
                'org.hamcrest:hamcrest-library:1.3')
        testRuntime 'cglib:cglib-nodep:3.1'
    }

    spotless {
        java {
            googleJavaFormat()
        }
    }

    findbugs {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

